# 构造数据抽象

![构造数据抽象.png](https://i.loli.net/2020/09/19/uhPNfOW8ZgKylen.png)

* 不同的数据对象组合起来，构成复合数据的方式，称为数据抽象。

## 1 抽象数据导引

### 1.1 有理数运算

* 对于有理数，直接使用分数来描述 + - * / 计算过程。

#### 1.1.1 序对

* 使用序对来描述一个有理数，分子代表序对的第一个元素，分母代表第二个元素。

    ```scheme
    (define (make-rat n d) (cons n d))
    ;;分子
    (define (numer x) (car x))
    ;;分母
    (define (denom x) (cdr x))
    ```

#### 1.1.2 有理数表示

* 使用分数计算规则来计算 + - * /
* 使用 print-rat 来表示一个有理数

  ```scheme
  define (add-rat x y)
  (make-rat (+ (* (numer x) (denom y))
              (* (numer y) (denom x)))
              (* (denom x) (denom y))))

  (define (sub-rat x y)
  (make-rat (- (* (numer x) (denom y))
              (* (numer y) (denom x)))
              (* (denom x) (denom y))))

  (define (mul-rat x y)
  (make-rat (* (numer x) (numer y))
              (* (denom x) (denom y))))

  (define (div-rat x y)
  (make-rat (* (numer x) (denom y))
              (* (denom x) (numer y))))
  ;;打印有理数
  (define (print-rat x)
  (newline)
  (display (numer x))
  (display "/")
  (display (denom x))
  )
  ```

### 1.2 过程表示数据

* 可以将过程作为对象去操作，使用过程来表示复合数据。

* 使用过程实现序对。

  ```scheme
  ;;返回一个过程(dispatch)，根据过程的参数取到复合数据
  (define (cons x y)
  (define (dispatch m)
      (cond ((= m 0) x)
          ((= m 1) y)
          (else (error "Argument not 0 or 1 -- CONS" m))))
  dispatch)
  (define (car z) (z 0))
  (define (cdr z) (z 1))
  ```

## 2 层次性数据和闭包 

* 序对为我们提供了一种构造复合数据的 __粘合剂__。

### 2.1 序列的表示

* 利用序对可以构造出 __序列__ :一批数据对象的一种有序集合(__list__)。

![序列.png](https://i.loli.net/2020/09/13/prSv2DsXCyb9fBh.png)

* 通过嵌套的cons形成的这样一个序对的序列称为 __表__

    ```scheme
    (cons 1
        (cons 2
                (cons 3
                    (cons 4 nil))))
    ```

* scheme 提供了基本操作list 来构造表，无需使用嵌套的 cons

    ```scheme
    ;;取表的第N个元素
    (define (list-ref items n)
    (if (= n 0)
        (car items)
        (list-ref (cdr items) (- n 1))
    )
    )
    ;;取表的长度
    (define (length items)
    (if (null? items)
        0
        (+ 1 (length (cdr items)))
    )
    )
    ```

### 2.2 序列的map计算

* __map__: 使用一个定义好的过程，逐次应用于序列表中的各个元素，返回结果表。 map 帮助我们建立起了一道屏障，将实现序列变换的过程和提取表中的元素等等细节隔离开。

    ```scheme
    ;;放大或缩小
    (define (scale-list items factor)
    (map (lambda (x) (* x factor))
        items))
    ```

### 2.3 序列的层次性结构

* 使用序对构成序列，可以推广到元素本身也是序列的序列，我们称之为 __树__

![树.png](https://i.loli.net/2020/09/19/2vVSkrQsE7mL19f.png)

* 序列本身也是序列的元素形成了 __子树__，__树叶__ 构成了元素本身。

* 统计树的树叶数目，pair 用于判断该节点是否是序对(子树)

    ```scheme
    (define (count-leaves x)
    (cond ((null? x) 0)
        ((not (pair? x)) 1)
        (else (+ (count-leaves (car x))
                 (count-leaves (cdr x))))
        )
    )
    ```

### 2.4 树的map运算

* 使用递归方案实现对树的运算

    ```scheme
    ;;树的map运算
    (define (scale-tree tree factor)
    (cond ((null? tree) nil)
            ((not (pair? tree)) (* tree factor))
            (else (cons (scale-tree (car tree) factor)
                        (scale-tree (cdr tree) factor)))))

    ;;数的map运算
    (define (scale-tree tree factor)
    (map (lambda (sub-tree)
            (if (pair? sub-tree)
                (scale-tree sub-tree factor)
                (* sub-tree factor)))
        tree))
    ```

### 2.5 序列的信号流结构

* 在复杂的计算过程中，我们可以使用序列作为一个步骤到下一个步骤的 __信号__ ，每个步骤的计算过程可以大致抽象为 __枚举器enumerate__ __过滤器filter__ __映射map__ __累积器accumulate__，这种设计可以大大提高代码的清晰度。

![序列的信号流.png](https://i.loli.net/2020/09/19/5lzCUJvdSgPupqQ.png)

* 计算出一个树的奇数的叶子平方和：

    ```scheme
    (define (enumerate-tree tree);;枚举叶子节点
    (cond ((null? tree) nil)
        ((not (pair? tree)) (list tree))
        (else (append (enumerate-tree (car tree))
                      (enumerate-tree (cdr tree))))))
    (define (filter predicate sequence) ;;过滤
    (cond ((null? sequence) nil)
            ((predicate (car sequence))
            (cons (car sequence)
                (filter predicate (cdr sequence))))
            (else (filter predicate (cdr sequence)))))
    ;: (filter odd? (list 1 2 3 4 5))

    ;: (map square (list 1 2 3 4 5))  ;;映射
    (define (accumulate op initial sequence)
    (if (null? sequence)
        initial
        (op (car sequence)
            (accumulate op initial (cdr sequence)))))
    ;: (accumulate + 0 (list 1 2 3 4 5))
    ```

## 3 符号数据

### 3.1 引号

* 通过给数值加引号，可以构造出符号本身的表，而不是数值本身的表。

    ```scheme
    (define a 1)
    (define b 2)
    (list a b)
    (list 'a 'b)
    ```

## 3.2 符号求导

* 该算法以一个符号表达式和一个变量作为参数，返回这个表达式相对于该变量的导数表达式。
  * 和式的求导，等于各个子项的导数相加。
  * 乘式的求导，等于各个子项的导数乘余项，并相加。

    ``` scheme
    (define (deriv exp var)
    (cond ((number? exp) 0)
            ((variable? exp)
            (if (same-variable? exp var) 1 0)
            )
            ((sum? exp) ;;和式
            (make-sum (deriv (addend exp) var)
                    (deriv (augend exp) var))
            )
            ((product? exp) ;;乘式
            (make-sum ;;构建和式
            (make-product (multiplier exp)
                            (deriv (multiplicand exp) var))
            (make-product (deriv (multiplier exp) var)
                            (multiplicand exp))
            )
            )
            (else
            (error "unknown expression type -- DERIV" exp))
        )
    )
    (define (variable? x) (symbol? x)) ;;是否是变量
    (define (same-variable? v1 v2) ;;是否都同一个变量
    (and (variable? v1) (variable? v2) (eq? v1 v2))
    )
    (define (make-sum a1 a2) (list '+ a1 a2)) ;;求和式
    (define (make-product m1 m2) (list '* m1 m2)) ;;求乘式
    (define (sum? x) ;;是否是和式
    (and (pair? x) (eq? (car x) '+))
    ) 
    (define (addend s) (cadr s)) ;;s 的被加数
    (define (augend s) (caddr s)) ;;s 的加数
    (define (product? x) ;;是否是乘式
    (and (pair? x) (eq? (car x) '*)))
    (define (multiplier p) (cadr p)) ;;p 的被乘数
    (define (multiplicand p) (caddr p)) ;;p的乘数
    ```

## 3.3 集合表示

* 一个集合就是一些不同对象的汇集

### 3.3.1 集合作为未排序的表

* 一个集合有如下操作，该集合中的数据是无序的。
  * union-set :集合并集。
  * intersection-set : 集合交集。
  * element-of-set?: 一个元素是否在集合中。
  * adjoin-set :加入一个元素到集合中。

   ```scheme
   (define (element-of-set? x set)
   (cond ((null? set) false)
           ((equal? x (car set)) true)
           (else (element-of-set? x (cdr set)))
    )
    )

    ;;添加元素需要遍历集合
   (define (adjoin-set x set)
   (if (element-of-set? x set)
       set
       (cons x set)))

    ;;交集也需要遍历集合 比较耗费时间
   (define (intersection-set set1 set2)
   (cond ((or (null? set1) (null? set2)) '())
           ((element-of-set? (car set1) set2)
           (cons (car set1)
               (intersection-set (cdr set1) set2)))
           (else (intersection-set (cdr set1) set2))))
    (define (iter input result)
    (if (null? input)
        (reverse result)
        (let ((current-element (car input))
              (remain-element (cdr input)))
            (if (element-of-set? current-element result)
                (iter remain-element result)
                (iter remain-element (cons current-element result))
            )
        )
    )
    )
    ```

### 3.3.2 集合作为排序的表

* element-of-set? 中，如果元素x比当前元素小，因为有序集合元素从小到大，所以元素x一定不在集合中。
* intersection-set 中，比较两个集合的起始元素，如果x1 = x2，那么得到了交集的第一个元素。如果 x1 < x2 因为x2 是最小的元素，立即可以断定x1 一定不再该集合中。

  ```scheme
  (define (element-of-set? x set)
    (cond ((null? set) false)
          ((= x (car set)) true)
          ((< x (car set)) false)
          (else (element-of-set? x (cdr set)))))

  (define (intersection-set set1 set2)
    (if (or (null? set1) (null? set2))
        '()
        (let ((x1 (car set1)) (x2 (car set2)))
          (cond ((= x1 x2)
                 (cons x1
                       (intersection-set (cdr set1)
                                         (cdr set2))))
                ((< x1 x2)
                 (intersection-set (cdr set1) set2))
                ((< x2 x1)
                 (intersection-set set1 (cdr set2)))))))
  ```

### 集合作为二叉树

* 如果使用集合作为一颗有序的二叉树，可以使查找的效率提升到对数级别。 使用 (list entry left right) ，表示一颗树的节点， 节点左边的数永远小于节点本身，节点右边的数永远大于本身。

![二叉搜索树.png](https://i.loli.net/2020/09/28/73clMtVzO8pE4Is.png)

* 二叉搜索树的查询效率依赖于数的平衡性，如果不短加入元素，二叉树就可能会退化成为单链表，导致查询速度过慢。

  ```scheme
  (define (entry tree) (car tree))
  (define (left-branch tree) (cadr tree))
  (define (right-branch tree) (caddr tree))
  (define (make-tree entry left right)
    (list entry left right))
  ;;遍历二叉搜索树
  (define (element-of-set? x set)
    (cond ((null? set) false)
          ((= x (entry set)) true)
          ((< x (entry set))
           (element-of-set? x (left-branch set)))
          ((> x (entry set))
           (element-of-set? x (right-branch set)))))
  ;;找到合适的位置，插入元素
  (define (adjoin-set x set)
    (cond ((null? set) (make-tree x '() '()))
          ((= x (entry set)) set)
          ((< x (entry set))
           (make-tree (entry set) 
                      (adjoin-set x (left-branch set))
                      (right-branch set)))
          ((> x (entry set))
           (make-tree (entry set)
                      (left-branch set)
                      (adjoin-set x (right-branch set))))))
  ```
